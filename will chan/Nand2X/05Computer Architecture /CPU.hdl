// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:

   
    And(a=instruction[12],b=instruction[15],out=a);
    And(a=instruction[11],b=instruction[15],out=zx);
    And(a=instruction[10],b=instruction[15],out=nx);
    And(a=instruction[9],b=instruction[15],out=zy);
    And(a=instruction[8],b=instruction[15],out=ny);
    And(a=instruction[7],b=instruction[15],out=f);
    And(a=instruction[6],b=instruction[15],out=no);
    And(a=instruction[5],b=instruction[15],out=WAI);
    And(a=instruction[4],b=instruction[15],out=WD);
    And(a=instruction[3],b=instruction[15],out=writeM);
    And(a=instruction[2],b=instruction[15],out=JL);
    And(a=instruction[1],b=instruction[15],out=JE);
    And(a=instruction[0],b=instruction[15],out=JG);
    Not(in=instruction[15],out=WAD);
    Or(a=WAI,b=WAD,out=WA);
 
    Mux16(a=instruction,b=lastres,sel=instruction[15],out=WTA);
    ARegister(in=WTA,load=WA,out=DFA,out[0..14]=addressM);
    DRegister(in=lastres,load=WD,out=NUM1);  
    Mux16(a=DFA,b=inM,sel=a,out=NUM2);
   
    ALU(x=NUM1,y=NUM2,zx=zx,nx=nx,zy=zy,ny=ny,f=f,no=no,out=lastres,zr=zr,ng=ng,out=outM);

    And(a=zr,b=JE,out=JEQ);
    And(a=ng,b=JL,out=JLT);
    Or(a=zr,b=ng,out=zan);
    Not(in=zan,out=ps);
    And(a=ps,b=JG,out=JGT);
    
    Or(a=JEQ,b=JLT,out=JLE);
    Or(a=JLE,b=JGT,out=JMP);

    PC(in=DFA,inc=true,load=JMP,reset=reset,out[0..14]=pc);
    
}